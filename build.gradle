buildscript {
    repositories {
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            name = "Maven Auth"
            url "https://repository.jboss.org/nexus/content/groups/public/"
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
    }
}

plugins {
    id 'java'
}

apply plugin: 'net.minecraftforge.gradle'

group 'net.impactdev'
version '1.1.1-SNAPSHOT'

subprojects {
    group 'net.impactdev.pixelmonbridge'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'net.minecraftforge.gradle'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    version '1.1.1-SNAPSHOT'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    minecraft {
        // The mappings can be changed at any time, and must be in the following format.
        // snapshot_YYYYMMDD   Snapshot are built nightly.
        // stable_#            Stables are built at the discretion of the MCP team.
        // Use non-default mappings at your own risk. they may not always work.
        // Simply re-run your setup task after changing the mappings to update your workspace.
        mappings channel: 'official', version: '1.16.5'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        minecraft 'net.minecraftforge:forge:1.16.5-36.2.20'

        compileOnly 'com.google.code.gson:gson:2.8.0'
        testImplementation group: 'junit', name: 'junit', version: '4.12'
    }

    processResources {
        // this will ensure that this task is redone when the versions change.
        inputs.property "version", project.version

        // replace stuff in mcmod.info, nothing else
        from(sourceSets.main.resources.srcDirs) {
            include 'mcmod.info'

            // replace version and mcversion
            expand 'version':project.version
        }

        // copy everything else, thats not the mcmod.info
        from(sourceSets.main.resources.srcDirs) {
            exclude 'mcmod.info'
        }
    }
}

apply plugin: 'maven-publish'

publishing {
    repositories {
        maven {
            name = "ImpactDev-Public"
            url = "https://maven.impactdev.net/repository/development/"
            credentials {
                username System.getenv('NEXUS_USER')
                password System.getenv('NEXUS_PW')
            }
        }
    }

    publications {
        common(MavenPublication) {
            from project.components.java
            groupId project.group
            artifactId "pixelmonbridge"
            version project.version
        }
    }
}

