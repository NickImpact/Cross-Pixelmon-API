buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "Maven Auth"
            url "http://repository.jboss.org/nexus/content/groups/public/"
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
        classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
    }
}

plugins {
    id 'java'
    id 'maven'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'
apply plugin: 'maven-publish-auth'

group 'net.impactdev'
version '1.0.3-SNAPSHOT'

subprojects {
    group 'net.impactdev.pixelmonbridge'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'net.minecraftforge.gradle'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    version '1.0.3-SNAPSHOT'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    minecraft {
        // The mappings can be changed at any time, and must be in the following format.
        // snapshot_YYYYMMDD   Snapshot are built nightly.
        // stable_#            Stables are built at the discretion of the MCP team.
        // Use non-default mappings at your own risk. they may not always work.
        // Simply re-run your setup task after changing the mappings to update your workspace.
        mappings channel: "stable", version: "39-1.12"
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly 'com.google.code.gson:gson:2.8.0'
        minecraft "net.minecraftforge:forge:1.12.2-14.23.5.2855"

        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

    processResources {
        // this will ensure that this task is redone when the versions change.
        inputs.property "version", project.version

        // replace stuff in mcmod.info, nothing else
        from(sourceSets.main.resources.srcDirs) {
            include 'mcmod.info'

            // replace version and mcversion
            expand 'version':project.version
        }

        // copy everything else, thats not the mcmod.info
        from(sourceSets.main.resources.srcDirs) {
            exclude 'mcmod.info'
        }
    }

}

publishing {
    repositories {
        maven {
            name = "ImpactDev-Public"
            url = "https://maven.impactdev.net/repository/development/"
            credentials {
                username System.getenv('NEXUS_USER')
                password System.getenv('NEXUS_PW')
            }
        }
    }

    publications {
        api(MavenPublication) {
            def project = project(":")
            from project.components.java
            groupId project.group
            artifactId "pixelmonbridge"
            version project.version
        }
        reforged(MavenPublication) { publication ->
            def project = project(":Reforged")
            from project.components.java
            groupId project.group
            artifactId "reforged"
            version project.version
        }
        generations(MavenPublication) { publication ->
            def project = project(":Generations")
            from project.components.java
            groupId project.group
            artifactId "generations"
            version project.version
        }
    }
}

